<!DOCTYPE html>
<html>
  <head>
    <title>BEKK-theme for remark</title>
    <meta http-equiv="Content-Type" content="text/html; charset=UTF-8"/>

    <script src="https://github.com/downloads/gnab/remark/remark-0.3.1.min.js" type="text/javascript">
      { "highlightStyle": "solarized_light" }
    </script>
    <script src="jquery-1.7.1.js" type="text/javascript"></script>

    <link href='http://fonts.googleapis.com/css?family=Inconsolata' rel='stylesheet' type='text/css'>

    <link rel="stylesheet/less" type="text/css" href="bekk.less">
    <script src="http://lesscss.googlecode.com/files/less-1.1.5.min.js" type="text/javascript"></script>
  </head>
  <body id="bekk">
    <textarea id="source">

.front-page

# CoffeeScript og Sass

## En introduksjon

Forelesning ved NITH

Kim Joar Bekkelund

18/11/2011

---

.agenda

# Agenda

* Lære CoffeeScript
* Forstå JavaScript
* Komme igang med JQuery
* Se litt på Sass

---

# CoffeeScript

Prosjektet utførte et sett sentrale aktiviteter for å effektivisere organisasjonen:

* Kompilerer ned til JavaScript.Kompilerer ned til JavaScript.Kompilerer ned til JavaScript.Kompilerer ned til JavaScript.
* … men er ikke et supersett (som Sass er for CSS).
* Mindre kode enn JavaScript.
* "Ny funksjonalitet" på toppen av JavaScript.
* Unngår de dårlige sidene av JavaScript, f.eks. globale variabler.
* Inspirert av Python og Ruby (dere vil kjenne igjen mye!)
* Default i Rails 3.1

---

.round

# Tester rundt bilde

---

.pushed

![Good times](https://github.com/kjbekkelund/remark-bekk/raw/master/images/details/_DSC2394.jpg)

# Boks i hjørnet

.red-circle-top-right[Spørsmål?]

Må finne en måte å kutte bildet

---

# JavaScript

* Det eneste programmeringsspråket som kjører i alle nettlesere.
* Brukes for å gjøre ting på klientsiden, f.eks. jobbe med DOM-en.
* Lang historie, men unngått av mange i flere år.
* Renessanse etter AJAX.
* Web 2.0
* Node.js

---

# Men hva er problemet med JavaScript?

Et Ruby-eksempel:

    .ruby
    names = people.map do |person|
      person.name.capitalize
    end

Og et JavaScript-eksempel:

    .javascript
    var names = []
    for (var i = 0, l = people.length; i &lt; l; i++) {
      var person = people[i];
      var name = person.name.slice(0,1).toUpperCase() +
                 person.name.slice(1);
      names.push(name);
    }

La oss gå gjennom noen av forskjellene.

---

# Litt mer seriøst

Det er lett å gjøre feil i JavaScript når man ikke kan språket skikkelig.

* Prototyper
* Globale variabler
* `==` vs `===`

Det er til og med en egen bok om hvordan bruke JavaScript rett.

---

.middle.center

# .yellow[Intro til] CoffeeScript

---

# CoffeeScript ≈ Ruby = ♥

Mye er likt Ruby, for eksempel:

* Ingen semikolon
* Trenger ikke paranteser for å kalle funksjoner
* Kommentarer starter med `#`
* Interpolasjon → `#{coffeescripting}`
* Implisitt `return`

Men CoffeeScript har noen særegenheter fra Ruby:

* Signifikant whitespace
* Ikke-blokkerende IO (Mindre skummelt enn det høres ut)

---

# Variabler

    .coffee
    myVariable = "test"
    someNumber = 42
    aBoolean = true

Helt likt Ruby — men camelcased.

---

# Funksjoner

I CoffeeScript brukes `->` for å definere en funksjon:

    .coffee
    foo = -> "bar"

Innholdet settes ofte på neste linje i lengre funksjoner:

    .coffee
    foo = ->
      "bar"

Med parameter:

    .coffee
    greet = (name) ->
      "hello #{name}!"

    alert greet("NITH")

---

# Funksjoner

Med default-variabler:

    .coffee
    greet = (name = "NITH") ->
      "hello #{name}!"

    alert greet()

Med *splats*:

    .coffee
    sum = (nums...) ->
      nums.reduce (a, b) -> a + b

    alert sum(1,2,3)
    nums = [1,2,3]
    alert sum nums...

---

# Funksjoner

Et velkjent Ruby-idiom er:

    .ruby
    def add_connection(host, options)
      port = options[:port]
      puts port
    end

    add_connection "localhost", :port => 2202
    add_connection "localhost", port: 2202 # Ruby 1.9

I CoffeeScript kan vi gjøre det samme:

    .coffee
    add_connection = (host, options) ->
      port = options.port
      alert port

    add_connection "localhost", port: 2202

---

# Funksjoner

Paranteser er valgfritt:

    .coffee
    sum = (a,b) -> a + b

    alert sum(5,4)
    alert sum 5,4

… men kun når en funksjon tar inn variabler:

    .coffee
    connection.close() # kaller funksjonen close
    connection.close   # returnerer funksjonen

---

# Førsteklasses funksjoner

I JavaScript kan man sende rundt funksjoner.

Altså kan en funksjon knyttes til en variabel, være input til en funksjon, returneres fra en funksjon, osv.

    .coffee
    close = (stuff) ->
      "closing #{stuff}"

    test = close

    alert test("db")

---

# Anonyme funksjoner

Det som egentlig har skjedd så langt er at vi har knyttet en variabel til en *anonym funksjon*.

Her knytter vi f.eks. variabelen `sum` til en funksjon som adderer to input-parametre.

    .coffee
    sum = (a,b) -> a + b

Selve funksjonen er altså kun:

    .coffee
    (a,b) -> a + b

---

# Deklarere objekter

Some å lage en hash i Ruby:

    .coffee
    father = {
      name: 'John',
      daughter: {
        name: 'Jill'
      },
      son: {
        name: 'Jack'
      }
    }

    alert father.son.name

---

# Deklarere objekter

La oss fjerne støyen:

    .coffee
    father =
      name: 'John'
      daughter:
        name: 'Jill'
      son:
        name: 'Jack'

    alert father.son.name

---

# Pattern matching

    .coffee
    list = ["Ruby", "Python", "JavaScript"]
    [head, tail...] = list
    alert head

Eller:

    .coffee
    list = ["Ruby", "Python", "JavaScript"]
    [head, rest..., bottom] = list
    alert bottom

Den er smart:

    .coffee
    list = ["Ruby", "JavaScript"]
    [head, rest..., bottom] = list
    alert head
    alert rest
    alert bottom

---

# Pattern matching på objekter

    .coffee
    me =
      name: "Kim Joar"
      company: "BEKK"

    { name, company } = me

    alert name
    alert company

---

# Ranges

Fungerer som i Ruby:

    .coffee
    alert [1..5]

Kan også brukes som i Ruby:

    .coffee
    users = ["Kim Joar", "Olav", "Sveinung", "Vetle"]
    alert users[0..2]
    alert users[1..]

Og det fungerer også på strenger:

    .coffee
    alert "test"[0..1]

---

# Iterere

Over arrays og ranges:

    .coffee
    nums = [1..10]

    for number in nums
      doStuffWith number

Med sjekk:

    .coffee
    nums = [1..10]

    for number in nums when number > 6
      doStuffWith number

---

# Iterere over objekter

(Legg merke til `of`, istedenfor `in`.)

    .coffee
    me = { name: "Kim Joar", company: "BEKK" }

    for key, value of me
      alert "#{key} = #{value}"

Også her kan vi gjøre en sjekk:

    .coffee
    me =
      name: "Kim Joar"
      company: "BEKK"
      statement: -> "hello world!"

    for key, value of me when typeof value is 'function'
      alert value()

---

# Sjekk at ting er tilstede eller satt

For å sjekke at et element er tilstede, kan man bruke `in`:

    .coffee
    frukter = ['eple', 'pære', 'banan']
    alert 'eple' in frukter

For å sjekke at en nøkkel er tilstede, kan man bruke `of`:

    .coffee
    me =
      name: "Kim Joar"
      company: "BEKK"

    alert 'name' of me
    alert 'statement' of me

---

# List Comprehensions

    .coffee
    doubles = (n * 2 for n in [1..5])
    alert doubles

Vi kan legge til et filter:

    .coffee
    large = (n for n in [1..5] when n > 2)
    alert large

Det sentrale med list comprehensions er at alle loop-er i CoffeeScript
returnerer en verdi, som er en array bestående av resultatet for hver
gjennomkjøring.

---

# if

Ganske likt Ruby:

    .coffee
    if person.name == "Kim Joar"
      alert "Yeah!"
    else
      alert "Bu!"

Det finnes en kort form:

    .coffee
    if ready() then attack() else wait()

Vi har også postfix-notasjon, som i Ruby:

    .coffee
    alert "great" if person.name == "Kim Joar"

---

# unless

Vi har unless også i CoffeeScript:

    .coffee
    alert "bu!" unless person.name == "Kim Joar"

# ?

Er en operator i CoffeeScript, ikke en del av funksjonsnavnet som i Ruby.

    .coffee
    practice() unless good_enough?

    c ?= d # er det samme som c = d unless c?

---

# Eventer

JavaScript fungerer annerledes enn Ruby. I Ruby kan du skrive:

    .ruby
    data = get_data()
    puts data

Men det samme ville ikke fungert i JavaScript.

Hvorfor?

---

# Eventer

Vi vet ikke om `get_data` er ferdig når vi prøver å skrive ut. JavaScript har
*ikke-blokkerende IO*.

I JavaScript (og CoffeeScript) sier vi:

1. Start gjennomføringen av denne funksjonen
2. Gå videre
3. Eventuelt, når du er ferdig, kall en funksjon (ofte kalt *callback*)

---

# Eventer

Istedenfor:

    .ruby
    data = get_data
    puts data

kunne vi i CoffeeScript ha skrevet:

    .coffee
    getData (data) ->
      console.log data

Her kaller vi funksjonen `getData` med en anonym funksjon som input. Dette er en *callback*.

---

# Lexical scoping

.gray[Psst, det er egentlig ganske simpelt og det er helt likt Ruby.]

    .coffee
    outer = 1
    changeNumbers = ->
      outer = 10

    changeNumbers()
    alert outer

Og hva skjer her?

    .coffee
    changeNumbers = ->
      outer = 10

    outer = 1
    changeNumbers()
    alert outer

---

# Lexical scoping

Grunnen til dette er at `outer` lever kun i funksjonen i det siste tilfellet. Det
kan vi se om vi gjør det følgende isteden:

    .coffee
    changeNumbers = ->
      outer = 10

    changeNumbers()
    alert outer

En variabel lever i et scope, som kan defineres på tre måter:

1. Enhver funksjon lager et scope.
2. En variabel lever i det ytterste scopet der den har blitt satt.
3. Utenfor sitt scope er variabelen usynlig.

---

# this

I Ruby bruker man `self` for å vise til objektet. I CoffeeScript vil dere se
`this` og `@` mange plasser, men ikke bland de med `self` i Ruby.

I CoffeeScript bruker man `this`, *ikke* for å vise til objektet, men for å
vise til *"this context"*.

La oss ta et eksempel.

---

# this

    .coffee
    settNavn = (navn) -> @navn = navn
    settNavn("Kim Joar")
    alert this.navn # Don't do this at home!

    # Kobler funksjonen til et person-objekt
    person = {}
    person.settNavn = settNavn
    person.settNavn("Kim Joar")
    alert person.navn

Her ser vi først navn satt på det *globale objektet*, og så satt på `person`.

Dette er *veldig* praktisk når vi har førsteklasses funksjoner.

---

# Kall en funksjon i en gitt kontekst

(Dette er litt avansert, men viser hvordan JavaScript fungerer.)

Ved å bruke funksjonen `apply` kan man kalle en funksjon i en gitt kontekst:

    .coffee
    settNavn = (navn) -> @navn = navn

    person = {}

    settNavn.apply person, ["Kim Joar"]

    alert person.navn

---

# Scope vs kontekst

Scope avhenger av hvor funksjonen er definert.

Kontekst avhenger av hvordan funksjonen blir kalt.

---

# Bind funksjonen (this == this)

Når du ønsker at en funksjon kjører i den nåværende konteksten, uansett hvordan den er kalt, kan du binde `this`.

    .coffee
    callback = (til) -> @mottakere.push(til)

Dersom denne funksjonen brukes som callback, vil `mottakere` sjekkes i det
objektet som *kaller* funksjonen, ikke der funksjonen er definert. Dette er
stress å fikse i JavaScript, men ikke i CoffeeScript. Løsningen:

    .coffee
    callback = (til) => @mottakere.push(til) # = istedenfor -

Et enkelt tegn. CoffeeScript ftw!

---

# Klasser

Som i Ruby brukes `class`. En klassen kan ha en `constructor` som kjøres når
man instansierer et objekt.

    .coffee
    class Person
      constructor: (name) ->
        @name = name

Legg merke til bruk av `:` istedenfor `=` som vi har brukt så langt. En klasse
er egentlig kun et objekt, som vi kan tenke på som en litt spesiell Ruby-hash.
Og til hver nøkkel knytter vi en anonym funksjon.

---

# Klasser

Det finnes en finere syntaks på konstruktøren når man bare skal sette en
variabel etterpå:

    .coffee
    class Person
      constructor: (@name) ->

Dette setter automagisk variabelen `@name` til input parameteren med samme navn.

---

# Properties

I en klasse har man *properties*, og det finnes to typer:

    .coffee
    class Person
      constructor: (@name) ->
        Person.count++

      # prototype properties
      say_hello: ->
        "Hello #{@name}"

      # class-level properties
      @count: 0

    kim = new Person("Kim Joar")
    mads = new Person("Mads")

    alert mads.say_hello()
    alert Person.count

Tenk på `self` i Ruby-klasser, så går det bra!

---

# Arv

    .coffee
    class Pet
      constructor: -> @isHungry = true
      eat: -> @isHungry = false

    class Dog extends Pet
      fetch: ->
        if @isHungry
          alert "nope!"
        else
          alert "yeah!"
        @isHungry = true

      eat: ->
        alert "yum!"
        super()

    dog = new Dog()
    dog.fetch()
    dog.eat()
    dog.fetch()

Som i Ruby kan du bruke `super`, så tar den med alle input-parametre.

---

# Kode tilgjengelig på tvers av filer

For å ikke innføre mye global kode, er default i CoffeeScript å holde all kode
privat innenfor hver `.coffee`-fil. Dere vil fort få behov for å dele kode mellom filer.

Den enkleste måten å dele kode, er å knytte en funksjon eller klasse til det
*global objektet*, som i nettleseren er `window`:

    .coffee
    # fil1.coffee
    class Person
      constructor: (@name) ->

    window.Person = Person

    # fil2.coffee
    alert Person   # Man trenger ikke skrive window.Person
    alert window.Person is Person

Legg så lite kode som mulig i det globale objektet!

---

# Komme igang med CoffeeScript

* Try CoffeeScript
* Kommandolinje (med en `coffee`-kommando)
* Rails 3.1! `gem 'coffee-rails'` og alle filer med `.coffee` i javascripts-mappen vil automatisk kompileres til JavaScript.

---

.middle.center

# JQuery

---

# Hvorfor JQuery?

* Cross-browser (a.k.a. håndtere .blue[IE])
* osv
* osv
* Sa jeg cross-browser?
* Og, så klart, et *veldig* bra API.

---

# JQuery manipulerer DOM-en

    .coffee
    $("#article p.big").html("test")

Input er (nesten) alltid en eller annen selektor (som egentlig bare er CSS + litt ekstra).

Når du har fått et JQuery-objekt kan du gjøre hva-som-helst — for eksempel
traversere, legge til, fjerne, flytte på, iterere, prepende, appende, scrolle,
osvosvosv.

---

# Et eksempel fra koden til denne presentasjonen

    .coffee
    $("code.coffee:contains('alert')")

Dette matcher da HTML-en som ser slik ut:

    .html
    <code class="coffee">
      kodekodekode
      alert
      kodekodekode
    </code>

---

# .bind kobler en funksjon til en event

Nettlesere har et sett med innebygde eventer, f.eks.:

* `click`
* `submit`
* `focus`
* `change`
* `mouseOver`

Vi kan dermed kalle en funksjon for eksempel når en bruker klikker på en link.

---

# Eksempel på bind med JQuery

    .coffee
    $('.shopping_cart').bind 'click', (event) =>
      @customer.purchase @cart

Vi har dermed bundet en funksjon til en klikk på et element. Her ser vi verdien av eventer og verdien av anonyme funksjoner.

Her er et eksempel der verdien av `this` ikke er bundet med `=>`:

    .coffee
    $('#foo').bind 'mouseenter mouseleave', (event) ->
      $(this).toggleClass('entered')

Her ser vi verdien av at `this` avhenger av konteksten.

---

.middle.center

Et av problemene med CoffeeScript er at man *burde* lære JavaScript.

(Debugging kan være vanskelig.)

---

# Komme igang med JavaScript

* [Understanding “Prototypes” in JavaScript](http://yehudakatz.com/2011/08/12/understanding-prototypes-in-javascript/)
* [Understanding JavaScript Function Invocation and "this"](http://yehudakatz.com/2011/08/11/understanding-javascript-function-invocation-and-this/)
* [JavaScript: The World's Most Misunderstood Programming Language](http://www.crockford.com/javascript/javascript.html)
* [What should every JavaScript programmer know?](http://stackoverflow.com/questions/2628672/what-should-every-javascript-programmer-know)

---

.middle.center

# .yellow[Intro til] Sass

Syntactically Awesome Stylesheets

---

# Vanlig .red[CSS]

… ser ofte ut som dette:

    .css
    #employee { padding: 0 0 10px 0; }
    #employee img { float: left; width: 175px; }
    #employee .info { float: left; width: 400px; }
    #employee .info div.row { font-size: 1.25em; }
    #employee .info .pdf { float: right; padding-top: 146px; }
    #employee .info .reference { float: right; padding-top: 146px; }

Hvorfor må vi gjenta det samme gang på gang?

---

# Variabler

Veldig ofte nevnes samme farger, størrelser og fonter mange ganger på
tvers av CSS-en.

    .sass
    $red: #FF4943;
    $primary-font: Georgia, serif;

    h1 { color: $red; font-family: $primary-font; }

Sass støtter også interpolering og funksjoner:

    .sass
    $side: top;
    $padding: 10px;
    $red: #FF4943;

    .box {
      padding-#{$side}: $size / 2;
      color: darken($red, 20%);
    }

---

# Nesting

Istedenfor:

    .css
    pre code .number {}
    pre code .keyword {}
    pre code .string {}
    pre code .function {}
    pre code .title {}
    pre code .params {}

Så kan vi ha:

    .sass
    pre code {
      .number {}
      .keyword {}
      .string {}
      .function {}
      .title {}
      .params {}
    }

---

# Mixins

Med mixins kan man enkelt trekke inn et sett med properties og selektorer:

    .sass
    @mixin rounded-corners($radius) {
      -moz-border-radius: $radius;
      -webkit-border-radius: $radius;
      border-radius: $radius;

      .content { padding: $radius + 5px; }
    }

    #footer { @include rounded-corners(10px); }

En mixin kan også bruke en annen mixin.

---

# Selector inheritance

Ofte skal en klasse ha de samme stilene som en annen klasse. Da brukes som
regel generelle klasser og mer spesifikke klasser, f.eks:

    .css
    .message { padding: 10px; border: 1px solid #ccc; }
    .error { background-color: red; }

Og så har man HTML-en:

    .html
    <div class="message error"></div>

Men med Sass kunne man hatt:

    .css
    .message { padding: 10px; border: 1px solid #ccc; }
    .error { @extend .message; background-color: red; }

---

# Hvordan det kan se ut

    .no-highlight
    $total-width: 575px;
    $img-width: 175px;

    #employee {
      padding: 0 0 10px 0;

      img { float: left; width: $img-width; }

      .info {
        float: left;
        width: $total-width - $img-width;

        .row { font-size: 1.25em; }

        .pdf { float: right; padding-top: 146px; }
        .reference { @extend .pdf; }
      }
    }


---

.agenda.middle

* Lære .red[CoffeeScript]
* Forstå .blue[JavaScript]
* Komme igang med .green[JQuery]
* Se litt på .yellow[Sass]

---

.middle.center

# Spørsmål?

Presentasjonen er laget med [remark](http://gnab.github.com/remark/).

    </textarea>
    <div id="slideshow"></div>
  </body>
    <script type="text/javascript">
      var remark = this.remark;
      $(document).ready(function() {
          remark.events.addListener("slidein", function() {
            $("h1").not($("h1 + span")).wrapInner("<span>");
            $(".pushed img").not($(".img > img")).parent().wrapInner("<span class='img'>")
          });
      });
    </script>
</html>
